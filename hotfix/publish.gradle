apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = "ray.rn"
version = "0.0.4"

def siteUrl = 'https://github.com/leiMsnow/RNHotFixSDK'
def issuesUrl = 'https://github.com/leiMsnow/RNHotFixSDK/issues'
def gitUrl = 'https://github.com/leiMsnow/RNHotFixSDK'

install {
    repositories.mavenInstaller.pom.project {
        packaging 'jar'
        name 'Hotfix'
        artifactId 'hotfix'
        description 'hot fix by react native.'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'always_0122'
                name 'always_0122'
                email 'always_0122@163.com'
            }
        }
    }
}

task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    boolean hasLocalFile = false
    if (project.rootProject.file('local.properties').exists()) {
        hasLocalFile = true
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = hasLocalFile ? properties.getProperty("bintray.user") : ""
    key = hasLocalFile ? properties.getProperty("bintray.apikey") : ""

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "hotfix"
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        labels = ["React Native", "HotFix", "Android"]
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}